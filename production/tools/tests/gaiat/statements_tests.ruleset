/////////////////////////////////////////////
// Copyright (c) Gaia Platform LLC
// All rights reserved.
/////////////////////////////////////////////

// RUN: %gaiat %s --

#include "tags_test.h"

// TESTCASE: for() with a table scan
ruleset test01
{
    on_insert(incubator)
    {
        float temp = 0;
        for (/I:incubator)
        {
            temp += I.max_temp;
        }
    }
}

// GAIAPLAT-1042 (fixed)
// TESTCASE: Use for statement to scan a table near the anchor.
ruleset test02
{
    on_insert(incubator)
    {
        float temp = 0;
        for (/S:sensor)
        {
            temp += S.value;
        }
    }
}

// TESTCASE: Use an implicit query in a for statement.
// NOTE: This implicit query from incubator to feeding goes through
//       raised and animal. This is the same distance as a path
//       that goes through farmer and yield. The selection of this
//       path is arbitrary, and would produce different results if
//       the other path was chosen. Should there be a warning?
// NOTE: Most recent decision is to make this an error. By rearranging
//       the order or table definitions in the DDL, the selection of
//       the implicit path may change - that would be bad. By making
//       this an error, the programmer can correct the code to have
//       an explicit component to the path.
// GAIAPLAT-1050 (fixed)
ruleset test03
{
    on_insert(incubator)
    {
        int total_portion = 0;
        for (animal->F:feeding)
        {
            total_portion += F.portion;
        }
    }
}

// TESTCASE: Use for statement to scan a table not near the anchor.
ruleset test04
{
    on_insert(incubator)
    {
        int total_portion = 0;
        for (/F:feeding)
        {
            total_portion += F.portion;
        }
    }
}

// TESTCASE: Path moves from M to 1 in if with nomatch.
// GAIAPLAT-1005 (fixed)
ruleset test05
{
    on_insert(sensor)
    {
        if (sensor->incubator.min_temp < 95.0)
        {
            sensor.value += 3.0;
        }
        nomatch
        {
            sensor.value += 5;
        }
    }
}

// TESTCASE: Path moves from M to 1 in if without nomatch.
ruleset test06
{
    on_insert(sensor)
    {
        if (sensor->incubator.min_temp < 95.0)
        {
            sensor.value += 3.0;
        }
    }
}

// TESTCASE: for() with an active field in the braces
ruleset test07
{
    {
        float temp = 0;
        // This looks like a loop, but the incubator is the anchor.
        for (incubator)
        {
            temp += @max_temp;
        }
    }
}

// TESTCASE: for-if with active field in the braces
ruleset test08
{
    {
        float temp = 0;
        for (I:incubator) if (I.min_temp < 50.0)
        {
            temp += @max_temp;
        }
    }
}

// TESTCASE: if() with active field in the braces
ruleset test09
{
    {
        float temp = 0;
        if (incubator.min_temp < 50.0)
        {
            temp += @max_temp;
        }
    }
}

// TESTCASE: if() with implicit iteration on an active field
ruleset test10
{
    {
        float temp = 0;
        if (incubator.min_temp < 50.0)
        {
            temp += @sensor.value;
        }
    }
}

// TESTCASE: if() with implicit iteration and on_insert
ruleset test11
{
    on_insert(incubator)
    {
        float temp = 0;
        if (incubator.min_temp < 50.0)
        {
            temp += sensor.value;
        }
    }
}

// TESTCASE: if-else with implicit iteration in both closures
ruleset test12
{
    on_insert(incubator)
    {
        float temp = 0;
        if (incubator.min_temp < 50.0)
        {
            temp += sensor.value;
        }
        else
        {
            temp -= sensor.value;
        }
    }
}

// TESTCASE: if-else with navigation
ruleset test13
{
    // Questionable code generation on this one.
    on_insert(incubator)
    {
        float temp = 0;
        // Navigates down to a child row (sensor), then back up to the parent (incubator),
        // and back down again to another child (actuator).
        if (sensor->incubator->actuator.value > 0.0)
        {
            temp += sensor.value;
        }
        else
        {
            temp -= sensor.value;
        }
    }
}

// TESTCASE: if-else-nomatch with navigation
ruleset test14
{
    on_insert(incubator)
    {
        float temp = 0;
        if (sensor->incubator->actuator.value > 0.0)
        {
            temp += sensor.value;
        }
// GAIAPLAT-947 (fixed)
        else
        {
            temp -= sensor.value;
        }
// GAIAPLAT-988 (fixed)
        nomatch
        {
            temp += 10.0;
        }
    }
}

// TESTCASE: if-else navigation with tags
ruleset test15
{
    on_insert(incubator)
    {
        float temp = 0;
        if (S:sensor->I:incubator->A:actuator.value > 0.0)
        {
            temp += S.value + I.min_temp + A.value;
        }
        else
        {
            temp -= I.max_temp - A.value - S.value;
        }
    }
}

// TESTCASE: if() and else-if() both with table scans and an active field in the if()
ruleset test16
{
    {
        int total_acres;
        if (@/F:farmer.acreage > 10)
        {
            total_acres = 10;
        }
        else if (/farmer.acreage < 5)
        {
            total_acres = 5;
        }
        else
        {
            total_acres = F.acreage;
        }
    }
}

// TESTCASE: if() with a table scan + active field and else-if() with navigation
ruleset test17
{
    {
        int total_acres;
        if (@/F:farmer.acreage > 10)
        {
            total_acres = 10;
        }
        else if (F->yield->crop.acres < 5)
        {
            total_acres = 5;
        }
        else
        {
            total_acres = F.acreage;
        }
    }
}

// TESTCASE: if-elseif tag scoping, using a function inside a declarative statement
int strcmp(const char*, const char*);

ruleset test18
{
    {
        int total_acres;
        if (@farmer->Y:yield->feeding.portion > 10)
        {
            total_acres = 10;
        }
        else if (strcmp(Y->farmer->raised.birthdate, "20140530") == 0)
        {
            total_acres = 5;
        }
        else
        {
            total_acres += Y->feeding.portion;
        }
    }
}

// TESTCASE: else-ifs with table scans unrelated to the on_insert anchor row's table.
ruleset test19
{
    on_insert(isolated)
    {
        int answer;
        if (/incubator->actuator.value > 0.5)
        {
            answer = 10;
        }
        else if (/farmer->yield.bushels > 10)
        {
            answer = 5;
        }
        else if (/raised->animal.age < 3)
        {
            answer += /feeding.portion;
        }
    }
}

// TESTCASE: multiple else-ifs with sequential nomatches.
ruleset test20
{
    on_insert(isolated)
    {
        int answer;
        if (/incubator->actuator.value > 0.5)
        {
            answer = 10;
        }
        else if (/farmer->yield.bushels > 10)
        {
            answer = 5;
        }
        else if (/raised->animal.age < 3)
        {
            answer += /feeding.portion;
        }
// GAIAPLAT-988 (fixed)
        nomatch
        {
            answer = 15;
        }
        nomatch
        {
            answer = 30;
        }
        nomatch
        {
            answer = 20;
        }
    }
}

// TESTCASE: single-cardinality reference in for()
ruleset test21
{
    on_insert(animal)
    {
        for (int i = 0; i < farmer.acreage; i++)
        {
            farmer.acreage++;
        }
    }
}

// TESTCASE: multiple-cardinality reference in for() with tag declaration
ruleset test22
{
    on_insert(incubator)
    {
        for (float f = 0.0; f < S:sensor.value; f += 1.3)
        {
            S.timestamp++;
        }
    }
}

// TESTCASE: braceless nested if-else-nomatches with implicit iteration
ruleset test23
{
    on_insert(incubator)
    {
        bool here = false;
        if (incubator.max_temp > 99.0)
            if (actuator.value < 97.3)
                if (sensor.value > 82.1)
                    here = true;
// GAIAPLAT-988 (fixed)
                nomatch
                    here = false;
            else
                here = false;
            nomatch
                here = true;
        nomatch
            here = false;
    }
}

// TESTCASE: prodcedural for() but with a query on all three of its header statements
ruleset test24
{
    on_insert(feeding)
    {
        // Another convoluted set of queries.
        // Note that the translated code is not yet correct.
        long some_number = 0;
        long i = 0;
        for (i += farmer.acreage; i < farmer->yield->feeding.portion; i += /crop.acres)
        {
            some_number += i;
        }
    }
}

// TESTCASE: expression with navigation on both sides
ruleset test25
{
    on_insert(feeding)
    {
        // Note that the translated code is not yet correct.
        farmer.condos->incubator->raised.birthdate += animal->crop->yield->feeding.portion;
    }
}

// TESTCASE: navigation with tags in function parameters
void sprintf(char*, const char*, const char*, float, float);

ruleset test26
{
    on_insert(raised)
    {
        char str[200];
        sprintf(str, "%ld,%f,%f", yield->farmer.incubators->I:incubator->raised.birthdate, I->A:actuator.value,
            A->incubator->sensor.value);
    }
}

// TESTCASE: declaring a tag in the on_insert statement
ruleset test27
{
    on_insert(I:incubator)
    {
        if (incubator.max_temp > 99.8)
        {
            I.min_temp -= 1.0;
        }
        nomatch
        {
            I.min_temp = 97.0;
        }
    }
}

// TESTCASE: implicit iteration in if-else-nomatch from a navigation path
ruleset test28
{
    on_insert(incubator)
    {
        float temp = 0;
        if (incubator->raised->animal->feeding.portion > 1)
        {
            temp += sensor.value;
        }
        else
        {
            temp -= sensor.value;
        }
        nomatch
        {
            temp += 10.0;
        }
    }
}

// TESTCASE: checking if procedural while() and if() still work
ruleset test29
{
    on_insert(incubator)
    {
        const char* s = "just a string";
        int i = 0;
        while (*s++)
        {
            max_temp += 1;
        }
        if (i < 5)
        {
            i = 1;
        }
    }
}

// TESTCASE: declarative statement inside a braceless procedural while()
ruleset test30
{
    on_insert(incubator)
    {
        const char* s = "just a string";
        int i = 0;
        while (*s++)
            max_temp += 1;
    }
}

// TESTCASE: delcaring a tag in an if() and using it in the else{} and nomatch{}
ruleset test31
{
    {
        int total_acres;
        if (@/F:farmer.acreage > 10)
        {
            total_acres = 10;
        }
        else if (/farmer.acreage < 5)
        {
            total_acres = 5;
        }
        else
        {
            total_acres = F.acreage;
        }
        nomatch
        {
            // This nomatch is for the inner 'if' so F is in scope
            total_acres = F.acreage;
        }
    }
}

// TESTCASE: procedural do-while that does not reference any rows
ruleset test32
{
    on_insert(incubator)
    {
        int count = 0;
        do
        {
            count += 1;
        }
        while (count < 10);
    }
}

// TESTCASE: procedural do-while referencing an active field on the anchor row
ruleset test33
{
    {
        int count = 0;
        do
        {
            count += 1;
        }
        while (@animal.age < 10);
    }
}

// TESTCASE: procedural do-while using a tag declared in the on_insert
ruleset test34
{
    on_insert(A:animal)
    {
        int count = 0;
        do
        {
            count += A.age;
        }
        while (A.age > 0);
    }
}

// TESTCASE: procedural do-while with a tag used in the do{} but not the while()
ruleset test35
{
    on_insert(A:animal)
    {
        int count = 0;
        do
        {
            count += A.age;
        }
        while (count < 10);
    }
}

// TESTCASE: declaring a tag in an if() and using it in a while()
ruleset test36
{
    {
        if (@C:crop.acres > 10)
        {
            int num = 0;
            while (C.acres > num)
            {
                num += C.acres;
            }
        }
    }
}

// TESTCASE: declaring a tag in an if() and using it in a do-while
ruleset test37
{
    {
        if (@C:crop.acres > 10)
        {
            int num = 0;
            do
            {
                num += C.acres;
            }
            while (C.acres > num);
        }
    }
}

// TESTCASE: declaring and using a tag inside the do{} of a do-while
ruleset test38
{
    on_insert(animal)
    {
        int num = 0;
        do
        {
            if(A:animal.age > 10)
            {
                num += A.age;
            }
        }
        while(num < 10);
    }
}

// TESTCASE: 1-to-1 navigation in a while() is legal
ruleset test39
{
    on_insert(incubator)
    {
        int count = 0;
        while (sensor.value > 0.0)
        {
            count += 1;
        }
    }

}

// TESTCASE: 1-to-1 navigation in a do-while is legal
ruleset test40
{
    on_insert(raised)
    {
        int num = 0;
        do
        {
            num++;
        }
        while(yield.bushels > 10 && num < 10);
    }
}

// TESTCASE: while() with navigation and tags
ruleset test41
{
    on_insert(incubator)
    {
        float temp = 0;
        while (S:sensor->I:incubator->A:actuator.value > 0.0)
        {
            temp += S.value + I.min_temp + A.value;
        }
    }
}

// TESTCASE: nested while() loops with navigation and tags
ruleset test42
{
    on_insert(incubator)
    {
        float temp = 0;
        while (S:sensor->I:incubator->A:actuator.value > 0.0)
        {
            temp += I.max_temp;
            while (A->II:incubator->SS:sensor.value < 100.0)
            {
                temp += S.value + II.min_temp + A.value;
            }
            temp -= S.value;
        }
    }
}

// TESTCASE: if() inside of while(), both with complex navigation, using on_insert
ruleset test43
{
    on_insert(farmer)
    {
        float temp = 0;
        while (farmer.incubators->incubator->raised->animal->feeding->yield->crop.acres)
        {
            if (farmer->yield->feeding->animal->raised->incubator.max_temp < 100.0)
            {
                temp += 1.0;
            }
        }
    }
}

// TESTCASE: if() inside of while(), both with complex navigation, active field in the while()
ruleset test44
{
    {
        float temp = 0;
        while (@farmer.condos->incubator->raised->animal->feeding->yield->crop.acres)
        {
            if (farmer->yield->feeding->animal->raised->incubator.max_temp < 100.0)
            {
                temp += 1.0;
            }
        }
    }
}

// TESTCASE: arbitrarily deep navigation with tags in a for() containing a while()
ruleset test45
{
    on_insert(incubator)
    {
        long tot_acres = 0;
        long tot_feed = 0;
        long tot_age = 0;
        for (int i = 0; i < /F:farmer->R:raised->A:animal->FE:feeding->Y:yield->C:crop.acres; i++)
        {
            tot_acres = animal->feeding->yield->crop.acres;
            while (A->FE2:feeding.portion > 0)
            {
                tot_feed += FE2.portion;
            }
            tot_age += Y->feeding->animal.age;
        }
    }
}

// TESTCASE: braceless while() with tag declarations
ruleset test46
{
    on_insert(incubator)
    {
        float temp = 0;
        while (S:sensor->I:incubator->A:actuator.value > 0.0)
            temp += S.value + I.min_temp + A.value;
    }
}

// TESTCASE: nested procedural and declarative while()s and an if()
ruleset test47
{
    on_insert(incubator)
    {
        const char* s = "just a string";
        int i = 0;
        while (*s++) {
            while (/incubator.max_temp > 0) {
                while(*s++) {
                    if (*s == ' ') {
                        break;
                    }
                }
            }
        }
    }
}

// TESTCASE: declarative do-while containing an if() that declares a tag
ruleset test48
{
    on_update(incubator)
    {
        int count = 0;
        do
        {
            if (A:actuator.value > 0.0)
            {
                count += 1;
            }
        }
        while (sensor.value > 0.0);
    }
}

// TESTCASE: declarative do-while with a full-table scan
ruleset test49
{
    on_insert(incubator)
    {
        int count = 0;
        do
        {
            count += 1;
        }
        while (/sensor.value > 0.0);
    }
}

// TESTCASE: do-while with an active field and a table scan in the while()
ruleset test50
{
    {
        int count = 0;
        do
        {
            count += 1;
        }
        while (@/animal.age < 10);
    }
}

// TESTCASE: do-while with a table scan in the while() over the child table of the anchor row's table
ruleset test51
{
    on_insert(incubator)
    {
        int count = 0;
        do
        {
            count += 1;
        }
        while (/sensor.value > 0.0);
    }
}

// TESTCASE: nested declarative do-while loops
ruleset test52
{
    on_insert(I:incubator)
    {
        int count_a = 0;
        do
        {
            int count_b = 0;

            do
            {
                int count_c = 0;

                do
                {
                    count_c += 1;
                } while (strcmp(raised.birthdate, "") != 0);

                count_b += 1;
            } while (actuator.value < 10);

            count_a += 1;
        }
        while (sensor.value > 0.0);
    }
}

// TESTCASE: arbitrarily complex navigation in the while() statement of a do-while
ruleset test53
{
    on_insert(farmer)
    {
        int count = 0;
        do
        {
            count++;
        }
        while (farmer->raised->animal->feeding->yield->crop.acres < 10);
    }
}

// TESTCASE: procedural for() with a declarative query inside a do-while
ruleset test54
{
    on_insert(incubator)
    {
        do
        {
            for (int i = 0; i < incubator.landlord->farmer.acreage; i++)
            {
                incubator.farmer->farmer.acreage++;
            }
        } while (sensor.value > 0.0);
    }
}

// TESTCASE: break from within a declarative for.
ruleset test57
{
    on_insert(incubator)
    {
        l:for (S:sensor) if (S.value > 99.9)
        {
            break l;
        }
        else
        {
            S.value += 0.5;
        }
    }
}

// TESTCASE: break from within a nested declarative for.
// GAIAPLAT-1056 (fixed)
ruleset test58
{
    on_insert(incubator)
    {
        for (S:sensor)
        {
            l:for (S->incubator.farmer->farmer->Y:yield)
            {
                if (Y.bushels > 10)
                {
                    break l;
                }
            }
            int i = 0;
        }
    }
}

void printf(const char*, unsigned int);

// TESTCASE: mix up declarative for with switch statement
// GAIAPLAT-1084 (fixed)
ruleset test59
{
    on_update(farmer)
    {
        for (F:feeding)
        {
            switch (F.portion)
            {
            default:
                F.portion += 1;
                printf("%d\n", F.portion);
            }
        }
    }
}

// TESTCASE: while with an anchor field condition
ruleset test60
{
    on_update(incubator)
    {
        while (incubator.max_temp < 100.0)
        {
            incubator.max_temp += 5.0;
        }
    }
}

ruleset test61
{
    on_update(incubator)
    {
        int maxtemp = incubator.max_temp;
        while (maxtemp < 100.0)
        {
            maxtemp += 5.0;
        }
        incubator.max_temp = maxtemp;
    }
}

ruleset test62
{
    on_update(incubator)
    {
        for (S:sensor)
        {
            while (S.value < 100.0)
            {
                S.value += 5.0;
            }
        }
    }
}

ruleset test63
{
    on_update(incubator)
    {
        for (S:sensor)
        {
            float val;
            val = S.value;
            while (val < 100.0)
            {
                val += 5.0;
            }
            S.value = val;
        }
    }
}

// TESTCASE: while() with an active field and a table scan
ruleset test64
{
    {
        int count = 0;
        while (@/animal.age < 10)
        {
            count += 1;
        }
    }
}

// TESTCASE: legal navigation in a do-while() statement
ruleset test65
{
    on_insert(farmer)
    {
        int count = 0;
        do
        {
            count++;
        }
        while (farmer->yield.bushels < 10);
    }
}

void printf(const char*, ...);

// check for meta-rule 2 compliance
// GAIAPLAT-1252
ruleset test66
{
    on_update(incubator.min_temp)
    {
        if (actuator.value > 50.0)
        {
            printf("%s, %ul\n", actuator.name, actuator.timestamp);
        }
    }
}

// TESTCASE: support initialization of declared variable with DAC object
// GAIAPLAT-966 (fixed)
ruleset test67
{
    on_insert(incubator)
    {
        float max = incubator.max_temp;
    }
}

unsigned int g_timestamp = 5;
float adjust_temperature(float, float, float, float);

// TESTCASE: expose translation problem that produces uncompilable C++ code
// GAIAPLAT-1204 (fixed)
ruleset test68
{
    on_update(S:sensor.value)
    {
        if (incubator.max_temp > 50.0)
        {
            if (!(sensor.value >= incubator.min_temp && sensor.value <= incubator.max_temp))
            {
                for(A:actuator)
                {
                    A.value = adjust_temperature(incubator.min_temp, incubator.max_temp, S.value, A.value);
                    A.timestamp = g_timestamp;
                }
            }
        }
    }
}

enum class e_timestamp_range_t : unsigned int
{
    beginning = 0,
    next      = 1,
    last      = 2
};

// TESTCASE: examine the translation of a switch statement without curly braces
// GAIAPLAT-1235
ruleset test69
{
    on_insert(incubator)
    {
        switch (e_timestamp_range_t(sensor.timestamp))
        {
            case e_timestamp_range_t::beginning : sensor.value = sensor.value + 1.0;  break;
            case e_timestamp_range_t::next      : sensor.value = sensor.value + 2.0;  break;
            case e_timestamp_range_t::last      : sensor.value = sensor.value + 3.0;  break;
        }
    }
}


// TESTCASE: examine the translation of a switch statement without curly braces, no looping
// GAIAPLAT-1235
ruleset test70
{
    on_insert(sensor)
    {
        switch (e_timestamp_range_t(sensor.timestamp))
        {
            case e_timestamp_range_t::beginning : sensor.value = sensor.value + 1.0;  break;
            case e_timestamp_range_t::next      : sensor.value = sensor.value + 2.0;  break;
            case e_timestamp_range_t::last      : sensor.value = sensor.value + 3.0;  break;
        }
    }
}

// TESTCASE: add an outer 'for' making sure it doesn't invalidate an inner 'for'
// GAIAPLAT-1247
#ifdef TEST_FAILURES
ruleset test71
{
    on_update(incubator)
    {
        for (/I:incubator)
        {
            float current_max = I.max_temp;
            float current_min = I.min_temp;
            if (current_max < current_min)
            {
                for (/II:isolated)
                {
                    II.age = 3;
                }
            }
        }
    }
}
#endif
